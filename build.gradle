buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:0.7.3'
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.2'
    }
}

apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.asciidoctor.gradle.asciidoctor'
apply plugin: 'com.bmuschko.tomcat'

sourceCompatibility = 1.8
version = '0.1'

def warFile = 'oms.war'

configurations {
    sshAntTask
    jdbcDriver
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

war {
    archiveName = warFile
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    // Spring Framework         
    compile 'org.springframework:spring-webmvc:4.1.6.RELEASE'
    compile 'org.springframework:spring-aspects:4.1.6.RELEASE'
    compile 'org.springframework:spring-test:4.1.6.RELEASE'
    compile 'org.springframework.data:spring-data-jpa:1.8.1.RELEASE'
    compile 'org.springframework:spring-context-support:4.1.6.RELEASE'
    compile 'org.springframework:spring-jms:4.1.6.RELEASE'

    // Hibernate
    compile 'org.hibernate:hibernate-entitymanager:4.3.9.Final'
    compile 'org.hibernate:hibernate-validator:5.1.3.Final'
    compile 'org.jadira.usertype:usertype.jodatime:2.0.1'
    
    // JMS
    compile 'org.apache.activemq:activemq-client:5.11.1'
    
    // Thymeleaf
    compile 'org.thymeleaf:thymeleaf-spring4:2.1.4.RELEASE'
    
    // Database
    compile 'mysql:mysql-connector-java:5.1.36'
    jdbcDriver 'mysql:mysql-connector-java:5.1.36'

    // Scheduler
    compile 'org.quartz-scheduler:quartz:2.2.1'
    
    // Testing
    testCompile 'org.springframework:spring-jdbc:4.1.6.RELEASE'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile 'org.apache.derby:derby:10.11.1.1'
    
    // Others
    compile 'org.apache.httpcomponents:httpclient:4.5'
    compile 'joda-time:joda-time:2.7'
    compile 'joda-time:joda-time-hibernate:1.4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.3'
    compile 'com.fasterxml.jackson.core:jackson-core:2.5.3'
    compile 'javax:javaee-web-api:7.0'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.4.0'
    runtime 'org.slf4j:slf4j-log4j12:1.7.12'
    runtime 'log4j:log4j:1.2.15'

    sshAntTask 'org.apache.ant:ant-jsch:1.9.5', 'com.jcraft:jsch:0.1.53'
    
    def tomcatVersion = '7.0.59'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

checkstyle {
    sourceSets = [sourceSets.main]
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

asciidoctor {
    backends = ['html5']
    baseDir = file("${projectDir}/src/asciidoc")
    outputDir = file("${projectDir}/src/main/webapp/doc")
    options = [
        attributes: [
            'revnumber': version
        ]
    ]
}

task remoteShutdownTomcat << {
    exec {
        executable "/usr/bin/ssh"
        args sshTargetHost, "'${sshTargetShutdownScript}'"
    }
}

task remoteStartTomcat << {
    exec {
        executable "/usr/bin/ssh"
        args sshTargetHost, "'${sshTargetStartupScript}'"
    }
}

task remoteDeploy(dependsOn: 'build') << {
    // Copy War file
    exec {
        executable "/usr/bin/scp"
        args "${buildDir}/libs/${warFile}", 
            "${sshTargetHost}:${sshTargetTomcatWebapp}/${warFile}"
    }
}